apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: '../quality/jacocoLibrary.gradle'
apply from: '../quality/ktlint.gradle'

android {
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools
    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.versionCode
        versionName Versions.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            buildConfigField "boolean", "USE_CRASHLYTICS", 'true'
        }
        debug {
            buildConfigField "boolean", "USE_CRASHLYTICS", 'false'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        // Always show the result of every unit test, even if it passes.
        unitTests.all {
            testLogging {
                events 'skipped', 'failed', 'standardOut', 'standardError'
            }
            reports.junitXml.destination = file('../build/test-results/')
        }
    }

    lintOptions {
        baseline file("lint-baseline.xml")
        warningsAsErrors true
        abortOnError true
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    api project(':core')

    // Support Libs
    api Libraries.appCompat
    api Libraries.recyclerView
    api Libraries.cardView
    api Libraries.gridLayout
    api Libraries.design
    api Libraries.v14
    api Libraries.dynamicAnims
    api Libraries.constraintLayout

    // Injection
    api Libraries.koinAndroid

    // Rx UI Bindings
    api Libraries.rxBindingV4
    api Libraries.rxBindingV7

    // Logging
    implementation(Libraries.firebaseCrashlytics)
    implementation(Libraries.firebaseAnalytics)

    // Utils
    api Libraries.dexter

    // Firebase
    implementation (Libraries.firebaseConfig) {
        exclude group:'com.google.protobuf', module: 'protobuf-lite'
    }

    // Unit Test dependencies
    testImplementation project(':testutils-android')
    testImplementation Libraries.junit
    testImplementation Libraries.mockito
    testImplementation Libraries.hamcrestJunit
    testImplementation Libraries.kotlinJunit
    testImplementation Libraries.kluent
    testImplementation(Libraries.mockitoKotlin) {
        ['kotlin-stdlib', 'kotlin-reflect'].each { exclude module: it }
    }
}
